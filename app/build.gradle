apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        // General config
        applicationId "ca.klapstein.baudit"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        // Testing config
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "google_maps_key", (project.findProperty("GOOGLE_MAPS_API_KEY") ?: "")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // misc android view/layout libraries
    // Up-to-date implementations to prevent collisions caused by play-services-maps
    implementation 'com.android.support:support-media-compat:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:28.0.0'

    // Google Maps Services
    implementation 'com.google.android.gms:play-services-maps:16.0.0'

    // Android instrument tests
    implementation 'com.android.support:design:28.0.0'

    // apache commons!
    implementation 'commons-validator:commons-validator:1.6'

    // Elastic Search
    implementation 'io.searchbox:jest-droid:2.4.0'

    // JetBrains annotations
    implementation 'org.jetbrains:annotations-java5:15.0'

    // android instrument tests
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    androidTestImplementation 'com.jayway.android.robotium:robotium-solo:5.6.3'

    // jvm unit tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.0'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // easy javadoc generation for android projects
        classpath "com.vanniktech:gradle-android-javadoc-plugin:0.3.0"
        // easy code coverage for android projects
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.3'
    }
}

apply plugin: "com.vanniktech.android.javadoc"
androidJavadoc {
    // outputDir return the documentation output dir
    // Default implementation :
    outputDir = { Project project ->
        "${project.rootDir}/docs/javadoc/"
    }
}

apply plugin: 'jacoco-android'
jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}

// allowing apache commons frameworks to exist in android project
configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}
